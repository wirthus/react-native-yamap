def DEFAULT_COMPILE_SDK_VERSION = 33
def DEFAULT_TARGET_SDK_VERSION = 33
def DEFAULT_MIN_SDK_VERSION = 23
def DEFAULT_GOOGLE_PLAY_SERVICES_VERSION = "21.3.0"

buildscript {
    ext {
        agp_version = '7.4.2'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
    }
}

apply plugin: "com.android.library"

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace "ru.vvdev.yamap"

    compileSdk safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-android:+'

    def googlePlayServicesVersion = safeExtGet('googlePlayServicesVersion', DEFAULT_GOOGLE_PLAY_SERVICES_VERSION)
    googlePlayServicesVersion = safeExtGet('playServicesVersion', googlePlayServicesVersion)
    googlePlayServicesVersion = safeExtGet('playServicesLocationVersion', googlePlayServicesVersion)

    implementation "com.google.android.gms:play-services-location:$googlePlayServicesVersion"

    implementation "com.yandex.android:maps.mobile:4.6.1-full"
}
